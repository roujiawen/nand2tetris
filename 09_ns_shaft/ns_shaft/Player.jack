
class Player {
    field int width, height, x0, y0, xInc, yInc, upInc, whichPlatform, numPlatforms;
    field int frameX0, frameX1, frameY0, frameY1;
    field boolean onPlatform;
    field Frame frame;
    field PlatformArray platforms;
    field Conveyor conveyor;
    field Game game;
    field int leftRightState; // 0: neutral; 1: left; 2: right
    field int animationTickTock;
    field int riseInterval;
    field int riseCount;
    
    constructor Player new(int x0_, int y0_, int upInc_, Game game_){
        var Frame frame;
        let frame = game_.getFrame();
        
        let frameX0 = frame.getX0();
        let frameX1 = frame.getX1();
        let frameY0 = frame.getY0();
        let frameY1 = frame.getY1();
        
        let width = 10;
        let height = 11;
        let xInc = 1;
        let yInc = 1;
        let upInc = upInc_;
        let x0 = x0_;
        let y0 = y0_;
        let onPlatform = false;
        let whichPlatform = 0;
        let platforms = game_.getPlatforms();
        let conveyor = game_.getConveyor();
        let game = game_;
        let leftRightState = 0;
        let animationTickTock = 0;
        
        let riseInterval = 40;
        let riseCount = 0;
        
        return this;
    }
    
    method void moveUp() {
        let y0 = y0 - (upInc * conveyor.move());
        if (y0 < frameY0) {
            do game.setGameOver(true);
        }
        return;
    }
    
    method void move() {
        var char key;
        let key = Keyboard.keyPressed();
        if (key=130) {
            // left 
            let x0 = Math.max(x0 - xInc, frameX0+1);
            let leftRightState = 1;
            return;
        }
        if (key=132) {
            // right
            let x0 = Math.min(x0 + xInc, frameX1-width);
            let leftRightState = 2;
            return;
        }
        
        let leftRightState = 0;
        // TODO: left right blocking by another platform
        return;
    }
    
    method void startRising() {
        let riseCount = riseInterval;
        return;
    }
    
    method void fall() {
        var int foot;
        var int p1, p2;
        var Platform nextPlatform;
        var boolean cond1, cond2, cond3;
        let nextPlatform = platforms.get(whichPlatform);
        
        if (riseCount > 0) {
            if (riseCount>10) {
                let y0 = y0 - (yInc * 2);
            } else {
                if (riseCount>3) {
                    let y0 = y0 - yInc;
                }
            }
            let onPlatform = false;
            // hit top?
            // roll back nextPlatform/whichPlatform
            let riseCount = riseCount - 1;
            return;
        }
        // detect falling
        if (onPlatform) {
            // detect if status changes
            if (~nextPlatform.support(x0, x0+width)) {
                //start falling
                let onPlatform = false;
                let y0 = y0 + yInc;
                // detect falling out of range
                let foot = y0+height;
                if (foot > frameY1) {
                    do game.setGameOver(true);
                    return;
                }
            }
        } else {
            // if not on a platform, keep falling
            let y0 = y0 + yInc;
            let foot = y0+height;
            // detect falling out of range
            if (foot > frameY1) {
                do game.setGameOver(true);
                return;
            }
            
            let p1 = platforms.getP1();
            let p2 = platforms.getP2();
            
            
            // update nextPlatform position within the platforms list
            // make sure nextPlatform still exists / hasn't been deleted
            let cond1 = whichPlatform < p1;
            let cond2 = p1 < p2;
            let cond3 = p2 < whichPlatform;
            while ((cond1 & cond2) |
                (cond2 & cond3) |
                (cond3 & cond1)) {
                    let whichPlatform = whichPlatform + 1;
                    do game.incScore();
                    if (whichPlatform = platforms.getMaxSize()) {
                        let whichPlatform = 0;
                    }
                    let nextPlatform = platforms.get(whichPlatform);
                    
                    let cond1 = whichPlatform < p1;
                    let cond2 = p1 < p2;
                    let cond3 = p2 < whichPlatform;
                }
            // otherwise continue:
            while ((nextPlatform.getY0() < foot) & (~(whichPlatform =p2))) {
                // go to the next platform
                // 1. inc index
                let whichPlatform = whichPlatform + 1;
                do game.incScore();
                // 2. (if index is at MaxSize i.e. p2-p1-w ) set index to 0
                if (whichPlatform = platforms.getMaxSize()) {
                    let whichPlatform = 0;
                }
                // 3. (if index is beyond p2) don't update nextPlatform
                if (~(whichPlatform =p2)) {
                    let nextPlatform = platforms.get(whichPlatform);
                }
            }
            
            if (~(whichPlatform =p2)) {
                // it must be that nextPlatform.getY0() >= foot
                // detect if reaches a platform
                // if same height
                if ((nextPlatform.sameHeight(foot, foot+yInc)) & 
                    (nextPlatform.support(x0, x0+width))) {
                    // stand on platform
                    let onPlatform = true;
                    do nextPlatform.onStanding();
                    if (nextPlatform.getType()=3) {
                        do startRising();
                    }
                } 
            }
        
            
        }
        
        return;
    }
        
    method void draw() {
        do Screen.setColor(false);
        
        if (conveyor.animationTick() = 1) {
            let animationTickTock = 1 - animationTickTock;
        }
        
        if (~onPlatform) {
            if (animationTickTock = 0) {
                do drawFallTick();
            } else {
                do drawFallTock();
            }  
            return;
        }
        
        if (leftRightState=0) { 
            do drawNeutral();
            return;
        }
        
        if (leftRightState=1) {
            if (animationTickTock = 0) {
                do drawLeftTick();
            } else {
                do drawLeftTock();
            }  
        } else {
            if (animationTickTock = 0) {
                do drawRightTick();
            } else {
                do drawRightTock();
            }  
        }
        
        // do Screen.setColor(false);
        // do Screen.drawRectangle(x0, y0, x0+width, y0+height);
        // do Screen.setColor(true);
        // do Screen.drawRectangle(x0+1, y0+1, x0+width-1, y0+height-1);
        return;
    }
    
    method void drawNeutral() {
        //Head
        do Screen.drawRectangle(x0+2, y0, x0+7, y0+2);
        do Screen.drawPixel(x0+1, y0+1);
        do Screen.drawPixel(x0+1, y0+2);
        do Screen.drawPixel(x0+8, y0+1);
        do Screen.drawPixel(x0+8, y0+2);
        //Body
        do Screen.drawRectangle(x0+3, y0+3, x0+6, y0+8);
        //L Arm
        do Screen.drawPixel(x0+2, y0+4);
        do Screen.drawPixel(x0+2, y0+5);
        do Screen.drawPixel(x0+1, y0+5);
        do Screen.drawPixel(x0+1, y0+6);
        do Screen.drawPixel(x0+0, y0+6);
        //R Arm
        do Screen.drawPixel(x0+7, y0+4);
        do Screen.drawPixel(x0+7, y0+5);
        do Screen.drawPixel(x0+8, y0+5);
        do Screen.drawPixel(x0+8, y0+6);
        do Screen.drawPixel(x0+9, y0+6);
        //L Foot
        do Screen.drawPixel(x0+3, y0+9);
        do Screen.drawPixel(x0+2, y0+8);
        do Screen.drawPixel(x0+2, y0+9);
        do Screen.drawPixel(x0+2, y0+10);
        do Screen.drawPixel(x0+1, y0+10);
        //R Foot
        do Screen.drawPixel(x0+6, y0+9);
        do Screen.drawPixel(x0+7, y0+8);
        do Screen.drawPixel(x0+7, y0+9);
        do Screen.drawPixel(x0+7, y0+10);
        do Screen.drawPixel(x0+8, y0+10);
        // Eyes
        do Screen.setColor(true);
        do Screen.drawPixel(x0+3, y0+1);
        do Screen.drawPixel(x0+6, y0+1);
        return;
    }
    
    method void drawFallTick() {
        //Head
        do Screen.drawRectangle(x0+2, y0, x0+7, y0+2);
        do Screen.drawPixel(x0+1, y0+1);
        do Screen.drawPixel(x0+1, y0+2);
        do Screen.drawPixel(x0+8, y0+1);
        do Screen.drawPixel(x0+8, y0+2);
        //Body
        do Screen.drawRectangle(x0+3, y0+3, x0+6, y0+8);
        //L Arm
        do Screen.drawPixel(x0+2, y0+4);
        do Screen.drawPixel(x0+2, y0+5);
        do Screen.drawPixel(x0+1, y0+5);
        do Screen.drawPixel(x0+1, y0+6);
        do Screen.drawPixel(x0+0, y0+6);
        //R Arm
        do Screen.drawPixel(x0+7, y0+4);
        do Screen.drawPixel(x0+7, y0+5);
        do Screen.drawPixel(x0+8, y0+5);
        do Screen.drawPixel(x0+8, y0+6);
        do Screen.drawPixel(x0+9, y0+6);
        //L Foot
        do Screen.drawPixel(x0+2, y0+8);
        do Screen.drawPixel(x0+2, y0+9);
        do Screen.drawPixel(x0+1, y0+8);
        do Screen.drawPixel(x0+1, y0+9);
        do Screen.drawPixel(x0+0, y0+9);
        //R Foot
        do Screen.drawPixel(x0+7, y0+8);
        do Screen.drawPixel(x0+7, y0+9);
        do Screen.drawPixel(x0+8, y0+8);
        do Screen.drawPixel(x0+8, y0+9);
        do Screen.drawPixel(x0+9, y0+9);
        // Eyes
        do Screen.setColor(true);
        do Screen.drawPixel(x0+3, y0+1);
        do Screen.drawPixel(x0+6, y0+1);
        return;
    }
    
    method void drawFallTock() {
        //Head
        do Screen.drawRectangle(x0+2, y0, x0+7, y0+2);
        do Screen.drawPixel(x0+1, y0+1);
        do Screen.drawPixel(x0+1, y0+2);
        do Screen.drawPixel(x0+8, y0+1);
        do Screen.drawPixel(x0+8, y0+2);
        //Body
        do Screen.drawRectangle(x0+3, y0+3, x0+6, y0+8);
        //L Arm
        do Screen.drawPixel(x0+2, y0+4);
        do Screen.drawPixel(x0+2, y0+5);
        do Screen.drawPixel(x0+1, y0+4);
        do Screen.drawPixel(x0+1, y0+5);
        do Screen.drawPixel(x0+0, y0+3);
        do Screen.drawPixel(x0+0, y0+4);
        //R Arm
        do Screen.drawPixel(x0+7, y0+4);
        do Screen.drawPixel(x0+7, y0+5);
        do Screen.drawPixel(x0+8, y0+4);
        do Screen.drawPixel(x0+8, y0+5);
        do Screen.drawPixel(x0+9, y0+3);
        do Screen.drawPixel(x0+9, y0+4);
        //L Foot
        do Screen.drawPixel(x0+2, y0+9);
        do Screen.drawPixel(x0+3, y0+9);
        do Screen.drawPixel(x0+2, y0+8);
        do Screen.drawPixel(x0+3, y0+10);
        //R Foot
        do Screen.drawPixel(x0+7, y0+9);
        do Screen.drawPixel(x0+7, y0+8);
        do Screen.drawPixel(x0+6, y0+9);
        do Screen.drawPixel(x0+6, y0+10);
        // Eyes
        do Screen.setColor(true);
        do Screen.drawPixel(x0+3, y0+1);
        do Screen.drawPixel(x0+6, y0+1);
        return;
    }
    
    method void drawLeftTick() {
        //Head
        do Screen.drawRectangle(x0+2, y0, x0+6, y0+2);
        do Screen.drawPixel(x0+7, y0+1);
        do Screen.drawPixel(x0+7, y0+2);
        do Screen.drawPixel(x0+1, y0+0);
        //Body
        do Screen.drawRectangle(x0+3, y0+3, x0+6, y0+8);
        //L Arm
        do Screen.drawPixel(x0+2, y0+4);
        do Screen.drawPixel(x0+2, y0+5);
        do Screen.drawPixel(x0+2, y0+6);
        do Screen.drawPixel(x0+1, y0+6);
        do Screen.drawPixel(x0+1, y0+7);
        do Screen.drawPixel(x0+0, y0+7);
        //R Arm
        do Screen.drawPixel(x0+7, y0+4);
        do Screen.drawPixel(x0+7, y0+5);
        do Screen.drawPixel(x0+7, y0+6);
        do Screen.drawPixel(x0+8, y0+6);
        do Screen.drawPixel(x0+8, y0+7);
        do Screen.drawPixel(x0+9, y0+7);
        //L Foot
        do Screen.drawPixel(x0+3, y0+9);
        do Screen.drawPixel(x0+2, y0+8);
        do Screen.drawPixel(x0+2, y0+9);
        do Screen.drawPixel(x0+1, y0+10);
        do Screen.drawPixel(x0+2, y0+10);
        do Screen.drawPixel(x0+3, y0+10);
        //R Foot
        do Screen.drawPixel(x0+6, y0+9);
        do Screen.drawPixel(x0+7, y0+8);
        do Screen.drawPixel(x0+7, y0+9);
        do Screen.drawPixel(x0+5, y0+10);
        do Screen.drawPixel(x0+6, y0+10);
        do Screen.drawPixel(x0+7, y0+10);
        // Eyes
        do Screen.setColor(true);
        do Screen.drawPixel(x0+3, y0+1);
        return;
    }
    
    method void drawLeftTock() {
        //Head
        do Screen.drawRectangle(x0+2, y0, x0+6, y0+2);
        do Screen.drawPixel(x0+7, y0+1);
        do Screen.drawPixel(x0+7, y0+2);
        do Screen.drawPixel(x0+1, y0+0);
        //Body
        do Screen.drawRectangle(x0+3, y0+3, x0+6, y0+10);
        //L Arm
        do Screen.drawPixel(x0+2, y0+4);
        do Screen.drawPixel(x0+2, y0+5);
        do Screen.drawPixel(x0+2, y0+6);
        do Screen.drawPixel(x0+2, y0+7);
        do Screen.drawPixel(x0+1, y0+7);
        //R Arm
        do Screen.drawPixel(x0+7, y0+4);
        do Screen.drawPixel(x0+7, y0+5);
        do Screen.drawPixel(x0+7, y0+6);
        do Screen.drawPixel(x0+7, y0+7);
        do Screen.drawPixel(x0+8, y0+7);
        //L Foot
        do Screen.drawPixel(x0+2, y0+10);
        do Screen.setColor(true);
        do Screen.drawPixel(x0+4, y0+9);
        //R Foot
        // Eyes
        do Screen.setColor(true);
        do Screen.drawPixel(x0+3, y0+1);
        return;
    }
    
    method void drawRightTick() {
        //Head
        do Screen.drawRectangle(x0+3, y0, x0+7, y0+2);
        do Screen.drawPixel(x0+2, y0+1);
        do Screen.drawPixel(x0+2, y0+2);
        do Screen.drawPixel(x0+8, y0+0);
        //Body
        do Screen.drawRectangle(x0+3, y0+3, x0+6, y0+8);
        //L Arm
        do Screen.drawPixel(x0+2, y0+4);
        do Screen.drawPixel(x0+2, y0+5);
        do Screen.drawPixel(x0+2, y0+6);
        do Screen.drawPixel(x0+1, y0+6);
        do Screen.drawPixel(x0+1, y0+7);
        do Screen.drawPixel(x0+0, y0+7);
        //R Arm
        do Screen.drawPixel(x0+7, y0+4);
        do Screen.drawPixel(x0+7, y0+5);
        do Screen.drawPixel(x0+7, y0+6);
        do Screen.drawPixel(x0+8, y0+6);
        do Screen.drawPixel(x0+8, y0+7);
        do Screen.drawPixel(x0+9, y0+7);
        //L Foot
        do Screen.drawPixel(x0+3, y0+9);
        do Screen.drawPixel(x0+2, y0+8);
        do Screen.drawPixel(x0+2, y0+9);
        do Screen.drawPixel(x0+2, y0+10);
        do Screen.drawPixel(x0+3, y0+10);
        do Screen.drawPixel(x0+4, y0+10);
        //R Foot
        do Screen.drawPixel(x0+6, y0+9);
        do Screen.drawPixel(x0+6, y0+10);
        do Screen.drawPixel(x0+7, y0+8);
        do Screen.drawPixel(x0+7, y0+9);
        do Screen.drawPixel(x0+7, y0+10);
        do Screen.drawPixel(x0+8, y0+10);
        // Eyes
        do Screen.setColor(true);
        do Screen.drawPixel(x0+6, y0+1);
        return;
    }
    
    method void drawRightTock() {
        //Head
        do Screen.drawRectangle(x0+3, y0, x0+7, y0+2);
        do Screen.drawPixel(x0+2, y0+1);
        do Screen.drawPixel(x0+2, y0+2);
        do Screen.drawPixel(x0+8, y0+0);
        //Body
        do Screen.drawRectangle(x0+3, y0+3, x0+6, y0+10);
        //L Arm
        do Screen.drawPixel(x0+2, y0+4);
        do Screen.drawPixel(x0+2, y0+5);
        do Screen.drawPixel(x0+2, y0+6);
        do Screen.drawPixel(x0+2, y0+7);
        do Screen.drawPixel(x0+1, y0+7);
        //R Arm
        do Screen.drawPixel(x0+7, y0+4);
        do Screen.drawPixel(x0+7, y0+5);
        do Screen.drawPixel(x0+7, y0+6);
        do Screen.drawPixel(x0+7, y0+7);
        do Screen.drawPixel(x0+8, y0+7);
        //L Foot
        //R Foot
        do Screen.drawPixel(x0+7, y0+10);
        do Screen.setColor(true);
        do Screen.drawPixel(x0+5, y0+9);
        // Eyes
        do Screen.setColor(true);
        do Screen.drawPixel(x0+6, y0+1);
        return;
    }


}