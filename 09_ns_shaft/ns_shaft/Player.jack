
class Player {
    field int width, height, x0, y0, xInc, yInc, upInc, whichPlatform, numPlatforms;
    field int frameX0, frameX1, frameY0, frameY1;
    field boolean onPlatform;
    field Frame frame;
    field PlatformArray platforms;
    field Conveyor conveyor;
    field Game game;
    field int slidingEffect, slidingInc;
    field int leftRightState; // 0: neutral; 1: left; 2: right
    field int animationTickTock;
    field int riseInterval;
    field int riseCount;
    
    constructor Player new(int x0_, int y0_, int upInc_, Game game_){
        var Frame frame;
        let frame = game_.getFrame();
        
        let frameX0 = frame.getX0();
        let frameX1 = frame.getX1();
        let frameY0 = frame.getY0();
        let frameY1 = frame.getY1();
        
        let width = 10;
        let height = 11;
        let xInc = 2;
        let yInc = 1;
        let upInc = upInc_;
        let x0 = x0_;
        let y0 = y0_;
        let onPlatform = false;
        let whichPlatform = 0;
        let platforms = game_.getPlatforms();
        let conveyor = game_.getConveyor();
        let game = game_;
        let slidingEffect = 0;
        let slidingInc = 1;
        let leftRightState = 0;
        let animationTickTock = 0;
        
        let riseInterval = 45;
        let riseCount = 0;
        
        return this;
    }
    
    method void moveUp() {
        let y0 = y0 - (upInc * conveyor.move());
        if (y0 < frameY0) {
            do game.setGameOver(true);
        }
        return;
    }
    
    method void move() {
        var char key;
        let key = Keyboard.keyPressed();
        
        let leftRightState = 0;
        
        if (key=130) {
            // left 
            let x0 = x0 - xInc;
            let leftRightState = 1;
        }
        if (key=132) {
            // right
            let x0 = x0 + xInc;
            let leftRightState = 2;
        }
        
        let x0 = Math.min(Math.max(x0 + slidingEffect, frameX0+1), frameX1-width);
        
        return;
    }
    
    method void startRising() {
        let riseCount = riseInterval;
        return;
    }
    
    method void fall() {
        var int foot;
        var int p1, p2;
        var int platformType;
        var Platform nextPlatform;
        var boolean cond1, cond2, cond3;
        let nextPlatform = platforms.get(whichPlatform);
        
        if (riseCount > 0) {
            if (riseCount>25) {
                let y0 = y0 - (yInc * 2);
            } else {
                if (riseCount>12) {
                    let y0 = y0 - yInc;
                }
            }
            let onPlatform = false;
            let foot = y0+height;
            let p1 = platforms.getP1();
            let p2 = platforms.getP2();
            
            // roll back nextPlatform/whichPlatform
            while ((nextPlatform.getY0() > (foot-1)) & (~(whichPlatform =p1))) {
                // go to the previous platform
                // 1. (if index is at 0 ) set index to MaxSize
                if (whichPlatform = 0) {
                    let whichPlatform = platforms.getMaxSize();
                }
                // 2. dec index
                let whichPlatform = whichPlatform - 1;
                // 3. update nextPlatform
                let nextPlatform = platforms.get(whichPlatform);
            }
            
            let riseCount = riseCount - 1;
            return;
        }
        // detect falling
        if (onPlatform) {
            // detect if status changes
            if (~nextPlatform.support(x0, x0+width)) {
                //start falling
                let slidingEffect = 0;
                let onPlatform = false;
                let y0 = y0 + yInc;
                // detect falling out of range
                let foot = y0+height;
                if (foot > frameY1) {
                    do game.setGameOver(true);
                    return;
                }
            }
        } else {
            // if not on a platform, keep falling
            let y0 = y0 + yInc;
            let foot = y0+height;
            // detect falling out of range
            if (foot > frameY1) {
                do game.setGameOver(true);
                return;
            }
            
            let p1 = platforms.getP1();
            let p2 = platforms.getP2();
            
            
            // update nextPlatform position within the platforms list
            // make sure nextPlatform still exists / hasn't been deleted
            let cond1 = whichPlatform < p1;
            let cond2 = p1 < p2;
            let cond3 = p2 < whichPlatform;
            while ((cond1 & cond2) |
                (cond2 & cond3) |
                (cond3 & cond1)) {
                    let whichPlatform = whichPlatform + 1;
                    if (whichPlatform = platforms.getMaxSize()) {
                        let whichPlatform = 0;
                    }
                    let nextPlatform = platforms.get(whichPlatform);
                    
                    let cond1 = whichPlatform < p1;
                    let cond2 = p1 < p2;
                    let cond3 = p2 < whichPlatform;
                }
            // otherwise continue:
            while ((nextPlatform.getY0() < foot) & (~(whichPlatform =p2))) {
                // go to the next platform
                // 1. inc index
                let whichPlatform = whichPlatform + 1;
                // 2. (if index is at MaxSize i.e. p2-p1-w ) set index to 0
                if (whichPlatform = platforms.getMaxSize()) {
                    let whichPlatform = 0;
                }
                // 3. (if index is beyond p2) don't update nextPlatform
                if (~(whichPlatform =p2)) {
                    let nextPlatform = platforms.get(whichPlatform);
                }
            }
            
            if (~(whichPlatform =p2)) {
                // it must be that nextPlatform.getY0() >= foot
                // detect if reaches a platform
                // if same height
                if ((nextPlatform.sameHeight(foot, foot+yInc)) & 
                    (nextPlatform.support(x0, x0+width))) {
                    // stand on platform
                    let onPlatform = true;
                    do nextPlatform.onStanding();
                    let platformType = nextPlatform.getType();
                    if (platformType=3) {
                        do startRising();
                        return;
                    }
                    if (platformType=4) {
                        let slidingEffect = -slidingInc;
                        return;
                    }
                    if (platformType=5) {
                        let slidingEffect = slidingInc;
                    }
                } 
            }
        
            
        }
        
        return;
    }
        
    method void draw() {
        do Screen.setColor(false);
        
        if (conveyor.animationTick() = 1) {
            let animationTickTock = 1 - animationTickTock;
        }
        
        if (~onPlatform) {
            if (animationTickTock = 0) {
                do DrawPlayer.fallTick(x0, y0);
            } else {
                do DrawPlayer.fallTock(x0, y0);
            }  
            return;
        }
        
        if (leftRightState=0) { 
            do DrawPlayer.neutral(x0, y0);
            return;
        }
        
        if (leftRightState=1) {
            if (animationTickTock = 0) {
                do DrawPlayer.leftTick(x0, y0);
            } else {
                do DrawPlayer.leftTock(x0, y0);
            }  
        } else {
            if (animationTickTock = 0) {
                do DrawPlayer.rightTick(x0, y0);
            } else {
                do DrawPlayer.rightTock(x0, y0);
            }  
        }
        return;
    }

}