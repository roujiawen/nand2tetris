
class Platform {
    field int type;
    field int width, height, x0, y0, upInc;
    field Conveyor conveyor;
    field int dissolveCountDown, dissolvePeriod;
    field int bounceCountDown, bouncePeriod;
    field int slidingStep, slidingStepLimit, slidingSlowDown;
    field int frameY0;
    field Game game;
    
    constructor Platform new(int type_, int x0_, int y0_, int width_, int height_, int upInc_, Game game_){
        var Frame frame;
        let frame = game_.getFrame();
        
        let type = type_; // 0: plain, 1: spiky, 2: dissolving, 3: bouncing, 4: slide left, 5: slide right
        let width = width_;
        let height = height_;
        let x0 = x0_;
        let y0 = y0_;
        let upInc = upInc_;
        let conveyor = game_.getConveyor();
        let frameY0 = frame.getY0();
        let dissolveCountDown = -1;
        let dissolvePeriod = 100;
        let bounceCountDown = 0;
        let bouncePeriod = 9;
        let slidingStep = 0;
        let slidingStepLimit = 12;
        let slidingSlowDown = 3;
        let game = game_;
        return this;
    }
        
    method void draw() {
        if (type = 0) {
            do DrawPlatform.plain(x0, y0, width, height);
            return;
        }
        if (type = 1) {
            do DrawPlatform.spiky(x0, y0, width, height);
            return;
        }
        if (type = 2) {
            if ((dissolveCountDown > (dissolvePeriod/2)) | (dissolveCountDown = -1)) {
                do DrawPlatform.dissolving(x0, y0, width, height);
                return;
            }
            
            if (dissolveCountDown > 0) {
                do DrawPlatform.dissolvingCracked(x0, y0, width, height);
                return;
            }
            return;
        }
        
        if (type = 3) {
            if (bounceCountDown>6) {
                do DrawPlatform.bouncingCompressed(x0, y0, width, height);
                return;
            }
            
            if (bounceCountDown>3) {
                do DrawPlatform.bouncing(x0, y0, width, height);
                return;
            }
            
            if (bounceCountDown>0) {
                do DrawPlatform.bouncingExpanded(x0, y0, width, height);
                return;
            }
            
            do DrawPlatform.bouncing(x0, y0, width, height);
            return;
        }
        
        if (type = 4) {
            do DrawPlatform.slidingLeft(x0, y0, width, height, slidingStep/slidingSlowDown);
            return;
        }
        
        if (type = 5) {
            do DrawPlatform.slidingRight(x0, y0, width, height, slidingStep/slidingSlowDown);
            return;
        }
        
        return;
    }
    
    method boolean moveUp() {
        let y0 = y0 - (upInc * conveyor.move());
        if ((type = 2) & (dissolveCountDown > 0)) {
            let dissolveCountDown = dissolveCountDown - 1;
        }
        
        if ((type = 3) & (bounceCountDown > 0)) {
            let bounceCountDown = bounceCountDown - 1;
        }
        
        if ((type = 4) | (type = 5)) {
            let slidingStep = slidingStep + 1;
            if (slidingStep > (slidingStepLimit*slidingSlowDown)) {
                let slidingStep = 0;
            }
        }
        if (y0 < frameY0) {
            return false;
        }
        return true;
    }
    
    method boolean dissolved() {
        return dissolveCountDown = 0;
    }
    
    method void onStanding() {
        if (type = 1) {
            do game.setGameOver(true);
        }
        if (type = 2) {
            let dissolveCountDown = dissolvePeriod;
        }
        if (type = 3) {
            let bounceCountDown = bouncePeriod;
        }
        return;
    }
    
    method int getY0() {
        return y0;
    }
    
    method int getType() {
        return type;
    }
    
    method boolean sameHeight(int yy0, int yy1) {
        if ((y0 < yy1)) {
            if ((yy0 < y0) | (yy0 = y0)) {
                return true;
            }
        }
        return false;    
    }
    
    method boolean support(int xx0, int xx1) {
        var int x1;
        if (dissolved()) {
            return false;
        }
        let x1 = x0 + width;
        if ((xx0 > x1) | (xx0 = x1)) {
            return false;
        }
        if ((xx1 < x0) | (xx1 = x0)) {
            return false;
        }
        return true;    
    }
    
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
    

}