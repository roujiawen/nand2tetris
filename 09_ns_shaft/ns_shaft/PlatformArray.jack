class PlatformArray {
    field int size;
    field int maxSize, paddedMaxSize;
    field Array arr;
    field int p1, p2;
    field int upInc;
    field int x0, paddedFrameWidth, y1;
    field int width, height;
    field Utils utils;
    field int coolOffPeriod;
    field int coolOffCount;
    field int probType0, probType1, probType2;
    field Game game;
    
    constructor PlatformArray new(int width_, int height_, int upInc_, Game game_){
        var Frame frame;
        let frame = game_.getFrame();
        
        let width = width_;
        let height = height_;
        let upInc = upInc_;
        
        let x0 = frame.getX0();
        let paddedFrameWidth = frame.getWidth()-width;
        let y1 = frame.getY1()-height;
        
        let maxSize = 30;
        let paddedMaxSize = 25;
        let arr = Array.new(maxSize);
        let size = 0;
        let p1 = 0;
        let p2 = 0;
        let utils = Utils.new();
        
        let coolOffPeriod = 80;
        let coolOffCount = 0;
        
        let probType0 = 70; //%
        let probType1 = 15; //%
        let probType2 = 15; //%
        
        let game = game_;
        
        return this;
    }
    
    method void newAt(int y, int type) {
        var Platform plt;
        let plt = Platform.new(type, x0+1+utils.randInt(paddedFrameWidth-1), y, width, height, upInc, game);
        let arr[p2] = plt;
        let p2 = p2 + 1;
        if (p2 = maxSize) {
            let p2 = 0;
        }
        let size = size + 1;
        let coolOffCount = 0;
        return;
    }
    
    method void addNewBottom(int prob) {
        // add new platform to the bottom with probability X/1000
        let coolOffCount = coolOffCount + 1;
        if (size = paddedMaxSize) {
            return;
        }
        if (coolOffCount < coolOffPeriod) {
            return;
        }
        if (utils.randInt(1000) < prob) {
            if (utils.randInt(100) < probType0) {
                do newAt(y1, 0);
            } else {
                if (utils.randInt(100) < probType1) {
                    do newAt(y1, 1); 
                } else {
                    if (utils.randInt(100) < probType2) {
                        do newAt(y1, 2);
                    } else {
                        do newAt(y1, 3);
                    }
                }
            }
            
        }
        return;
    }
    
    method Platform get(int i) {
        return arr[i];
    }
    
    method int getP1() {
        return p1;
    }
    
    method int getP2() {
        return p2;
    }
    method int getSize() {
        return size;
    }
    
    method int getMaxSize() {
        return maxSize;
    }
    
    method void moveUp() {
        var int i, numOps;
        var Platform plt;
        var boolean success;
        
        let i = p1;
        let numOps = size;
        
        while (numOps > 0) {
            let plt = arr[i];
            
            let success = plt.moveUp();
            if (~success) {
                // delete platform
                do plt.dispose();
                let p1 = p1 + 1;
                let size = size - 1;
                if (p1 = maxSize) {
                    let p1 = 0;
                }
                // there needs to be at least 1 platform
                if (size =0) {
                    do addNewBottom(1000);
                }
            }
            let i = i + 1;
            if (i = maxSize) {
                let i = 0;
            }
            let numOps = numOps - 1;
        }
        return;
    }
    
    
    method void draw() {
        var int i, numOps;
        var Platform plt;
        
        let i = p1;
        let numOps = size;
        
        while (numOps > 0) {
            let plt = arr[i];
            do plt.draw();
            
            let i = i + 1;
            if (i = maxSize) {
                let i = 0;
            }
            let numOps = numOps - 1;
        }
        return;
    }


}